TIME -> 0 = GOTO -> HITBOX_INIT, TA_INIT, CREATE_DESK


//// STATE ////

$ -> HITBOX_INIT = HITBOX -> -4, -3, 20, 19
$ -> TA_INIT = TRIPLE_ACTION -> PLAY*TRY_PLAYING
$ -> CREATE_DESK = CREATE_DECORATION -> 13-9-FRONT:WORLD,3,2,DESK_UP


//// TRY PLAYING ////

$ -> TRY_PLAYING = GOTO -> CUTSCENE:START, PLAYER_DROP_GLASS, AFFECTING:OBJ-19*UPDATE_CHAIR_SIT, BLOCKING_PATH:FALSE, PLAYER_GOING_ON_SEAT, WAITING_BEFORE_GUILTYNESS
$ -> PLAYER_GOING_ON_SEAT = STATE_PLAYER -> 14-9,0,4,DOWN,SIT, SOUND:JUMP
$ -> WAITING_BEFORE_GUILTYNESS = WAITFOR -> TIMER:1 ? AFFECTING:PLAYER*EXPRESSION_PLAYER_SUSPENSION, WAITING_BEFORE_TALKING
$ -> WAITING_BEFORE_TALKING = WAITFOR -> TIMER:2 ? GET_DIALOGUE
$ -> CANCEL_PLAYING = GOTO -> CUTSCENE:STOP, PLAYER_EXIT_SEAT, AFFECTING:OBJ-19*UPDATE_CHAIR
$ -> PLAYER_EXIT_SEAT = STATE_PLAYER -> 14-9,0,0,DOWN,GOOD, BLOCKING_PATH:TRUE


//// REFRESH DESK ////

! -> REFRESH_DESK = GOTO -> DELETE_DESK, CREATE_DESK, REFRESH_GLASS
$ -> DELETE_DESK = DELETE -> 13-9:WORLD,1


//// DROP GLASS ////

$ -> PLAYER_DROP_GLASS = CONDITION_PLAYER -> GLASS ? CREATE_GLASS_HANDLER / NULL
$ -> CREATE_GLASS_HANDLER = GOTO -> NAME:GLASS, AFFECTING:HAS_NO_WATER, A2_TAKE_GLASS, CONDITION_PLAYER:GOOD, CREATE_GLASS, SAVE_GLASS_ON_DESK, SOUND:GLASS
$ -> CREATE_GLASS = CREATE_DECORATION -> 13-10-FRONT:WORLD,1,1,GLASS_02
$ -> SAVE_GLASS_ON_DESK = SAVE -> REGISTER, REBECCAS_ROOM*GLASS_ON_DESK
$ -> REFRESH_GLASS = NAME -> GLASS ? CREATE_GLASS / NULL


//// TAKE GLASS ////

! -> HAS_WATER = ACTION_TWO -> NULL
! -> HAS_NO_WATER = NAME -> GLASS ? A2_TAKE_GLASS / NULL
$ -> A2_TAKE_GLASS = ACTION -> 2,TAKE_GLASS*TAKE_GLASS
$ -> TAKE_GLASS = GOTO -> NAME:CHAIR, AFFECTING:HAS_WATER, DELETE_GLASS, CONDITION_PLAYER:GLASS, UNSAVE_GLASS_ON_DESK, SOUND:GLASS
$ -> DELETE_GLASS = DELETE -> 13-10:WORLD,10
$ -> UNSAVE_GLASS_ON_DESK = SAVE -> CANCEL_REGISTER, REBECCAS_ROOM*GLASS_ON_DESK


//// SEARCHING KEY ////

! -> KNOWING_KEY_POS = ACTION -> 3,NULL
! -> SEARCHING_KEY = ACTION -> 3,SEARCH_KEY*KEY_SEARCH
$ -> KEY_SEARCH = GOTO -> MEMORY:SEARCHING_KEY_ON_DESK, TRY_PLAYING
$ -> TALK_KEY_SEARCH_CONDITIONNAL = MEMORY -> KEY_SECOND_ATTEMPT ? MEMORY:KNOWING_KEY_POS, AFFECTING:KNOWING_KEY_POS, TALK:KEY_FOUND_DESK / FIRST_ATTEMPT_CONDITIONNAL, TALK:KEY_SEARCH_DESK
$ -> FIRST_ATTEMPT_CONDITIONNAL = MEMORY -> KEY_FIRST_ATTEMPT ? MEMORY:KEY_SECOND_ATTEMPT / MEMORY:KEY_FIRST_ATTEMPT


//// DIFFERENT DIALOGUES ////

$ -> GET_NO_SARAH_DIALOGUE = MEMORY -> SEARCHING_KEY_ON_DESK ? SECOND_VERIFICATION / GET_FIRST_DIALOGUE
$ -> SECOND_VERIFICATION = MEMORY -> HAS_SEARCHED_KEY_ON_DESK ? GET_FIRST_DIALOGUE / TALK_KEY_SEARCH_CONDITIONNAL
TALKED -> KEY_FOUND_DESK = GOTO -> MEMORY:HAS_SEARCHED_KEY_ON_DESK, CANCEL_PLAYING
TALKED -> KEY_SEARCH_DESK = GOTO -> MEMORY:HAS_SEARCHED_KEY_ON_DESK, CANCEL_PLAYING

$ -> GET_FIRST_DIALOGUE = MEMORY -> FIRST_TIME_PLAYING ? GET_SECOND_DIALOGUE / FIRST_DIALOGUE
$ -> FIRST_DIALOGUE = GOTO -> TALK:CANT_PLAY, ACTION_ONE:PLAY_ANYWAY*TRY_PLAYING
TALKED -> CANT_PLAY = GOTO -> MEMORY:FIRST_TIME_PLAYING, CANCEL_PLAYING, STOP_JOKING_IF_TOO_FAR

$ -> GET_SECOND_DIALOGUE = MEMORY -> SECOND_TIME_PLAYING ? GET_THIRD_DIALOGUE / SECOND_DIALOGUE
$ -> SECOND_DIALOGUE = GOTO -> TALK:CANT_PLAY_SECOND, ACTION_ONE:JUST_ONE_GAME*TRY_PLAYING
TALKED -> CANT_PLAY_SECOND = GOTO -> MEMORY:SECOND_TIME_PLAYING, CANCEL_PLAYING, STOP_JOKING_IF_TOO_FAR

$ -> GET_THIRD_DIALOGUE = MEMORY -> THIRD_TIME_PLAYING ? GET_FOURTH_DIALOGUE / THIRD_DIALOGUE
$ -> THIRD_DIALOGUE = GOTO -> TALK:CANT_PLAY_THIRD, ACTION_ONE:I_AM_WEAK*TRY_PLAYING
TALKED -> CANT_PLAY_THIRD = GOTO ->  MEMORY:THIRD_TIME_PLAYING, CANCEL_PLAYING, STOP_JOKING_IF_TOO_FAR

$ -> GET_FOURTH_DIALOGUE = MEMORY -> FOURTH_TIME_PLAYING ? NULL / FOURTH_DIALOGUE
$ -> FOURTH_DIALOGUE = GOTO -> TALK:CANT_PLAY_FOURTH
TALKED -> CANT_PLAY_FOURTH = GOTO ->  MEMORY:FOURTH_TIME_PLAYING, CANCEL_PLAYING

$ -> STOP_JOKING_IF_TOO_FAR = WAITFOR -> PLAYER_FUTHER_THAN:3 ? ACTION_ONE:NULL


//// SARAH INTERACTION ////

$ -> GET_DIALOGUE = MEMORY -> SARAH_INTERACTION ? PARSE_SARAH_LEFT / GET_NO_SARAH_DIALOGUE
$ -> PARSE_SARAH_LEFT = MEMORY -> SARAH_LEFT ? GET_NO_SARAH_DIALOGUE / TALK:CANT_PLAY_SARAH
TALKED -> CANT_PLAY_SARAH = GOTO -> CANCEL_PLAYING
! -> SARAH_LEFT = MEMORY -> FIRST_TIME_PLAYING ? NULL / ACTION_ONE:PLAY*TRY_PLAYING