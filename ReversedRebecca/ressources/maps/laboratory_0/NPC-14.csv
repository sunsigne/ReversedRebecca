TIME -> 0 = GOTO -> NAME:JOSEPH, FACING:RIGHT, PLAYER_AVOIDER_TYPE:AROUND_OMNISCIENT


//// STATE ////

$ -> SIT = GOTO -> WAITFOR_NULL, AFFECTING:OBJ-36*BLOCKING_VERTICAL_COUCH_FALSE, FACING:RIGHT, SOUND_IF_CAMERA:JUMP, TP:9-31, CONDITION:SIT
$ -> DISPLAY_NORMAL = DISPLAY -> 0, 0
$ -> DISPLAY_CRY = DISPLAY -> 1, 0
$ -> DISPLAY_SURPRISED = DISPLAY -> 0, -1


//// PLAYERLESS BEHAVIOR ////

! -> START_LEVEL = WAITFOR -> TIMER:120 ? SLOW:10-31
GOAL -> 10-31 = GOTO -> SIT, A1_COUCH, PARSE_JOSEPH_SLEEPY_BEFORE_COUCH
$ -> PARSE_JOSEPH_SLEEPY_BEFORE_COUCH = MEMORY -> JOSEPH_SLEEPY_BEFORE_COUCH ? AFFECTING:OBJECT*JOSEPH_FEELS_SLEEPY / AFFECTING:MARIE*MARIE_THINKING_ABOUT_COFFEE
! -> JOSEPH_FEELS_SLEEPY = WAITFOR -> TIMER:20 ? PARSE_IS_CRYING
$ -> PARSE_IS_CRYING = CONDITION -> CRY ? NULL / WAITFOR_PLAYER_FAR_ENOUGH
$ -> WAITFOR_PLAYER_FAR_ENOUGH = WAITFOR -> PLAYER_FUTHER_THAN:5 ? ACTION_ONE:NULL, DISPLAY_NORMAL, CONDITION:BED
$ -> WAITFOR_NULL = WAITFOR -> NULL
$ -> TA_NULL = TRIPLE_ACTION -> NULL


//// BEFORE COUCH ////

! -> SCENARIO_EXIT = TRIPLE_ACTION -> TALK_VAR*JOSEPH_BLA_BLA_CONDITIONNAL
! -> SCENARIO_INQUIRY = TRIPLE_ACTION -> TALK*JOSEPH_HAVE_YOU_DATA
TALKED -> JOSEPH_HAVE_YOU_DATA = FACING -> RIGHT
! -> THEFT = ACTION_TWO -> THEFT*TALK_JOSEPH_THEFT
$ -> TALK_JOSEPH_THEFT = TALK -> JOSEPH_THEFT
$ -> JOSEPH_BLA_BLA_CONDITIONNAL = MEMORY -> REBECCA_ASKED_FOR_SAS_KEY_TO_JOSPEH ? TALK:JOSEPH_BLA_BLA / TALK:JOSEPH_BLA_BLA_HELLO
TALKED -> JOSEPH_BLA_BLA = TALK -> JOSEPH_BLA_BLA_NEXT
TALKED -> JOSEPH_BLA_BLA_HELLO = TALK -> JOSEPH_BLA_BLA_NEXT
TALKED -> JOSEPH_BLA_BLA_NEXT = GOTO -> MEMORY:REBECCA_MET_JOSEPH_BEFORE_COUCH, FACING:RIGHT, ACTION_ONE:TALK_VAR*TALK_JOSEPH_FORGOT
$ -> TALK_JOSEPH_FORGOT = MEMORY -> ALICIA_FOLLOW_PLAYER ? AFFECTING:ALICIA*TP_TO_PLAYER, TALK:JOSEPH_FORGOT / TALK:JOSEPH_FORGOT
TALKED -> JOSEPH_FORGOT = GOTO -> FACING:RIGHT, WAITFOR_TRYING_TO_REMEMBER
$ -> WAITFOR_TRYING_TO_REMEMBER = WAITFOR -> TIMER:59 ? PARSE_SEE_PLAYER
$ -> PARSE_SEE_PLAYER = SEE -> PLAYER ? PARSE_CUTSCENE_RUNNING / MEMORY:JOSEPH_REMEMBERED_THEN_FORGET_AGAIN, SLOW:10-31
$ -> PARSE_CUTSCENE_RUNNING = CUTSCENE -> RUNNING ? PARSE_SEE_PLAYER / PARSE_CHOOSING
$ -> PARSE_CHOOSING = CHOOSING -> TRUE ? PARSE_SEE_PLAYER / JOSPEH_REMEMBER_QUESTION
$ -> JOSPEH_REMEMBER_QUESTION = GOTO -> CUTSCENE:START, EXPRESSION:EXCLAMATION, WAITFOR_HEADING_PLAYER
$ -> WAITFOR_HEADING_PLAYER = WAITFOR -> TIMER:1 ? HEADING_PLAYER
$ -> HEADING_PLAYER = MEMORY -> ALICIA_FOLLOW_PLAYER ? SLOW:ALICIA, WAITFOR_REACHED_PLAYER / SLOW:PLAYER, WAITFOR_REACHED_PLAYER
$ -> WAITFOR_REACHED_PLAYER = WAITFOR -> PLAYER_CLOSER_THAN:3 ? SLOW:PLAYER, WAITFOR_TALKING_ABOUT_REMEMBER
$ -> WAITFOR_TALKING_ABOUT_REMEMBER = WAITFOR -> TIMER:1 ? FACING_PLAYER:JOSEPH, TALK:JOSEPH_REMEMBER
TALKED -> JOSEPH_REMEMBER = MEMORY -> REBECCA_ASKED_FOR_SAS_KEY_TO_JOSPEH ? CUTSCENE:STOP, PARSE_IS_JOSEPH_ON_COUCH / TALK:JOSEPH_REMEMBER_AND_KEY
TALKED -> JOSEPH_REMEMBER_AND_KEY = GOTO -> KEY_CONVERSATION
$ -> KEY_CONVERSATION = GOTO -> SOUND:LOOT_SPAWN, CREATE_GREEN_KEY_HANDLER, TALK:JOSEPH_PINKY
$ -> CREATE_GREEN_KEY = CREATE -> 12-31-FRONT:1-2-255
TALKED -> JOSEPH_PINKY = GOTO -> PREVENT_A3_SAS_KEY, CUTSCENE:STOP, PARSE_IS_JOSEPH_ON_COUCH
$ -> PARSE_IS_JOSEPH_ON_COUCH = MEMORY -> JOSEPH_REMEMBERED_THEN_FORGET_AGAIN ? MEMORY:TA_JOSEPH_REFUSE, A1_COUCH / SLOW:10-31


//// DURING COUCH ////

$ -> A1_COUCH = MEMORY -> SCENARIO_EXIT ? ACTION_ONE:TALK_VAR*TALK_ON_COUCH_CONDITIONNAL / NULL
$ -> TALK_ON_COUCH_CONDITIONNAL = MEMORY -> REBECCA_MET_JOSEPH_BEFORE_COUCH ? PARSE_JOSEPH_REMEMBERED_THEN_FORGET_AGAIN / TALK_JOSEPH_HELLO_CONDITIONNAL
$ -> TALK_JOSEPH_HELLO_CONDITIONNAL = MEMORY -> REBECCA_ASKED_FOR_SAS_KEY_TO_JOSPEH ? TALK:JOSEPH_HELLO / TALK:JOSEPH_HELLO_HELLO
$ -> PARSE_JOSEPH_REMEMBERED_THEN_FORGET_AGAIN = MEMORY -> JOSEPH_REMEMBERED_THEN_FORGET_AGAIN ? PARSE_TA_JOSEPH_REFUSE / TALK:JOSEPH_REFUSE
$ -> PARSE_TA_JOSEPH_REFUSE = MEMORY -> TA_JOSEPH_REFUSE ? TALK:JOSEPH_REFUSE / TALK:JOSEPH_REMEMBERED_THEN_FORGET_AGAIN
TALKED -> JOSEPH_REMEMBERED_THEN_FORGET_AGAIN = MEMORY -> REBECCA_ASKED_FOR_SAS_KEY_TO_JOSPEH ? CUTSCENE:STOP, PARSE_IS_JOSEPH_ON_COUCH / TALK:JOSEPH_REMEMBER_AND_KEY
TALKED -> JOSEPH_REFUSE = GOTO -> ACTION_ONE:OFFER_A_TEA*JOSEPH_OFFER_A_TEA
TALKED -> JOSEPH_HELLO_HELLO = GOTO -> ACTION_ONE:TALK_VAR*TALK_JOSEPH_FORGOT_COUCH
TALKED -> JOSEPH_HELLO = GOTO -> ACTION_ONE:TALK_VAR*TALK_JOSEPH_FORGOT_COUCH
$ -> TALK_JOSEPH_FORGOT_COUCH = MEMORY -> ALICIA_FOLLOW_PLAYER ? AFFECTING:ALICIA*TP_TO_PLAYER, TALK:JOSEPH_FORGOT_COUCH / TALK:JOSEPH_FORGOT_COUCH
TALKED -> JOSEPH_FORGOT_COUCH = GOTO -> MEMORY:TA_JOSEPH_REFUSE, ACTION_ONE:TALK*JOSEPH_REFUSE
$ -> JOSEPH_OFFER_A_TEA = MEMORY -> ALICIA_FOLLOW_PLAYER ? AFFECTING:ALICIA*TP_TO_PLAYER_COUCH, TALK:JOSEPH_OFFER_A_TEA / TALK:JOSEPH_OFFER_A_TEA
TALKED -> JOSEPH_OFFER_A_TEA = GOTO -> DISABLE_ONTHESPOT:2, CHOICE_TEA
$ -> CHOICE_TEA = CHOICE -> I_HAVE_TO_GO*JOSEPH_POLITELY_TAKE_LEAVE, SHUT_UP*JOSEPH_BLOW_HIM_OFF
! -> JOSEPH_POLITELY_TAKE_LEAVE = GOTO -> ENABLE_ONTHESPOT:2, TALK:JOSEPH_OFFER_A_TEA_GOOD
! -> JOSEPH_BLOW_HIM_OFF = GOTO -> ENABLE_ONTHESPOT:2, TALK:JOSEPH_OFFER_A_TEA_BAD
TALKED -> JOSEPH_OFFER_A_TEA_BAD = GOTO -> MEMORY:JOSEPH_CRIED, DISPLAY_CRY, CONDITION:CRY, WAITFOR_PLAYER_FAR_ENOUGH
TALKED -> MARIE_INTRUDER = CONDITION -> SIT ? AFFECTING:OBJECT*JOSEPH_FEELS_SLEEPY / MEMORY:JOSEPH_SLEEPY_BEFORE_COUCH


//// SAS KEY ////

! -> INIT_GREEN_SAS_DOORS = GOTO -> PREVENT_A3_SAS_KEY
UNLOCKEDTOOL -> KEY:GREEN = GOTO -> PREVENT_A3_SAS_KEY
! -> DELTA_INFORMED_PLAYER_THAN_JOSPEH_HAS_SAS_KEY = GOTO -> MEMORY:JOSEPH_A3_IS_ABOUT_SAS_KEY, A3_SAS_KEY_CONDITIONNAL
$ -> A3_SAS_KEY_CONDITIONNAL = MEMORY -> JOSEPH_AND_MARIE_ARE_TALKING ? A3_SAS_KEY_CONDITIONNAL_NEXT / A3_SAS_KEY_DOUBLE_CHECKED
$ -> A3_SAS_KEY_CONDITIONNAL_NEXT = MEMORY -> JOSEPH_AND_MARIE_NO_LONGER_TALKING ? A3_SAS_KEY_DOUBLE_CHECKED / MEMORY:JOSEPH_SHOULD_BE_ASKED_BACK_ABOUT_KEY
$ -> A3_SAS_KEY_DOUBLE_CHECKED = MEMORY -> NO_MORE_JOSEPH_A3_KEY ? NULL / A3_SAS_KEY
$ -> A3_SAS_KEY = ACTION_THREE -> SAS_KEY*TALK_JOSEPH_SAS_KEY_CONDITIONNAL
$ -> TALK_JOSEPH_SAS_KEY_CONDITIONNAL = MEMORY -> JOSEPH_CRIED ? TALK:JOSEPH_SAS_KEY_BAD / PARSE_BED_TALK_JOSEPH_SAS_KEY
$ -> PARSE_BED_TALK_JOSEPH_SAS_KEY = CONDITION -> BED ? TALK:JOSEPH_SAS_KEY_SLEEPING / TALK:JOSEPH_SAS_KEY
TALKED -> JOSEPH_SAS_KEY_BAD = GOTO -> CREATE_SAS_KEY
TALKED -> JOSEPH_SAS_KEY = GOTO -> CREATE_SAS_KEY
$ -> PREVENT_A3_SAS_KEY = MEMORY -> JOSEPH_A3_IS_ABOUT_SAS_KEY ? MEMORY:NO_MORE_JOSEPH_A3_KEY, ACTION_THREE:NULL / MEMORY:NO_MORE_JOSEPH_A3_KEY
TALKED -> JOSEPH_SAS_KEY_SLEEPING = GOTO -> FACING:DOWN, CONDITION:SIT, DISPLAY_SURPRISED, TALK:JOSEPH_SAS_KEY_SLEEPING_NEXT
TALKED -> JOSEPH_SAS_KEY_SLEEPING_NEXT = GOTO -> CREATE_SAS_KEY, WAITFOR_SLEEPING_PRE_BACK
$ -> WAITFOR_SLEEPING_PRE_BACK = WAITFOR -> TIMER:3 ? EXPRESSION:SUSPENSION, WAITFOR_SLEEPING_BACK
$ -> WAITFOR_SLEEPING_BACK = WAITFOR -> TIMER:3 ? CONDITION:BED, DISPLAY_NORMAL
$ -> PARSE_JOSEPH_SHOULD_BE_ASKED_BACK_ABOUT_KEY = MEMORY -> JOSEPH_SHOULD_BE_ASKED_BACK_ABOUT_KEY ? A3_SAS_KEY_DOUBLE_CHECKED / NULL
$ -> CREATE_SAS_KEY = GOTO -> MEMORY:REBECCA_ASKED_FOR_SAS_KEY_TO_JOSPEH, CREATE_GREEN_KEY_HANDLER
$ -> CREATE_GREEN_KEY_HANDLER = GOTO -> SOUND:LOOT_SPAWN, CREATE_GREEN_KEY, CREATE_SAS_KEY_CHECKER, MEMORY:NO_MORE_JOSEPH_A3_KEY, MEMORY:TALK_DELTA_LOST_NEXT_ALREADY_KEY, AFFECTING:OBJ-49*INIT_SAS_KEY_CHECKER
$ -> CREATE_SAS_KEY_CHECKER = CREATE -> 5-27-FRONT:49-255-255


//// JOSEPH MARIE ////

! -> TA_MARIE_JOSEPH_TALK = GOTO -> SAVED_TRIPLE_ACTION:SAVE, TA_NULL, MEMORY:JOSEPH_AND_MARIE_ARE_TALKING
! -> TA_STOP_MARIE_JOSEPH_TALK = GOTO -> SAVED_TRIPLE_ACTION:LOAD, MEMORY:JOSEPH_AND_MARIE_NO_LONGER_TALKING, PARSE_JOSEPH_SHOULD_BE_ASKED_BACK_ABOUT_KEY